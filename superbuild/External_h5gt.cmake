set(proj h5gt)
message("external project: ${proj}")

# SET DIRS
set(EP_SOURCE_DIR "${CMAKE_BINARY_DIR}/${proj}")
set(EP_BINARY_DIR "${CMAKE_BINARY_DIR}/${proj}-build")
set(EP_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/${proj}-install")
list(APPEND CMAKE_PREFIX_PATH ${EP_INSTALL_DIR})

#-----------------------------------------------------------------------------
set(h5gt_ROOT ${EP_INSTALL_DIR})

set(DEPENDENCIES Eigen3 ZLIB HDF5)

if(NOT DEFINED COPY_H5GTPY_RUNTIME_DEPS)
  set(COPY_H5GTPY_RUNTIME_DEPS OFF)
endif()

if(NOT DEFINED RESOLVE_H5GTPY_RUNTIME_DEPS)
  set(RESOLVE_H5GTPY_RUNTIME_DEPS ON)
endif()

ExternalProject_Add(${proj}
  GIT_REPOSITORY "https://github.com/TierraColada/h5gt.git"
  GIT_TAG "b5c5f702ddb9803d802ae7baefd5da564e005c6c"
  SOURCE_DIR ${EP_SOURCE_DIR}
  BINARY_DIR ${EP_BINARY_DIR}
  INSTALL_DIR ${EP_INSTALL_DIR}
  CMAKE_CACHE_ARGS
    # CMake settings
    -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
    -DCMAKE_CXX_STANDARD:STRING=${CMAKE_CXX_STANDARD}
    -DCMAKE_CXX_STANDARD_REQUIRED:BOOL=${CMAKE_CXX_STANDARD_REQUIRED}
    -DCMAKE_CXX_EXTENSIONS:BOOL=${CMAKE_CXX_EXTENSIONS}
    -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
    # GNUInstallDirs expands CMAKE_INSTALL_LIBDIR to 'lib' or to'lib64' depending on the OS
    -DCMAKE_INSTALL_LIBDIR:PATH=lib
    # Lib settings
    -DH5GT_USE_EIGEN:BOOL=ON
    -DH5GT_USE_XTENSOR:BOOL=OFF
    -DH5GT_USE_OPENCV:BOOL=OFF
    -DH5GT_UNIT_TESTS:BOOL=OFF
    -DH5GT_EXAMPLES:BOOL=OFF
    -DH5GT_PARALLEL_HDF5:BOOL=OFF
    -DH5GT_BUILD_h5gtpy:BOOL=${H5GEO_BUILD_h5geopy}
    -DHDF5_RUNTIME_DIRS:PATH=${HDF5_RUNTIME_DIRS}
    -DZLIB_RUNTIME_DIRS:PATH=${ZLIB_RUNTIME_DIRS}
    -DCOPY_H5GTPY_RUNTIME_DEPS:BOOL=${COPY_H5GTPY_RUNTIME_DEPS}
    -DRESOLVE_H5GTPY_RUNTIME_DEPS:BOOL=${RESOLVE_H5GTPY_RUNTIME_DEPS}
    -DHDF5_USE_STATIC_LIBRARIES:BOOL=OFF
    -DEigen3_ROOT:PATH=${Eigen3_ROOT}
    -DZLIB_ROOT:PATH=${ZLIB_ROOT}
    -DHDF5_ROOT:PATH=${HDF5_ROOT}
    # h5gt and h5geo use pybind11 (PYTHON_EXECUTABLE)
    -DPYTHON_EXECUTABLE:PATH=${PYTHON_EXECUTABLE}
  DEPENDS ${DEPENDENCIES}
  )
